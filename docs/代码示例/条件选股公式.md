<script async src="https://pagead2.googlesyndication.com/pagead/js/adsbygoogle.js"></script>
<!-- 展示广告3 -->
<ins class="adsbygoogle"
     style="display:block"
     data-ad-client="ca-pub-6890694312814945"
     data-ad-slot="8321470275"
     data-ad-format="auto"
     data-full-width-responsive="true"></ins>
<script>
     (adsbygoogle = window.adsbygoogle || []).push({});
</script>

!!! note "巴菲特选股"
    * A1赋值:(营业收入-营业成本 )/营业收入*100>N1
    * A2赋值:净利润/营业收入*100>N2
    * A3赋值:净利润/股东权益(净资产)*100>N3
    * A1 AND A2 AND A3

    ``` tdx
    A1:=(FINANCE(20)-FINANCE(21) )/FINANCE(20)*100>N1;{销售毛利率}
    A2:=FINANCE(30)/FINANCE(20)*100>N2;{净利率}
    A3:=FINANCE(30)/FINANCE(19)*100>N3;{净资产收益率}
    A1 AND A2 AND A3;
    ```
!!! note "老鸭头"
    * E1赋值:收盘价的13日指数移动平均
    * E2赋值:收盘价的55日指数移动平均
    * A1赋值:统计5日中满足E1<1日前的E1的天数>=3 AND E1>1日前的E1
    * A2赋值:统计13日中满足E2>1日前的E2的天数>=8 AND E2>1日前的E2
    * A3赋值:13日内(最低价/E2-1)的最低值<=0.1
    * A4赋值:统计13日中满足E1>E2的天数=13
    * A5赋值:统计5日中满足收盘价>E2的天数=5
    * A6赋值:收盘价上穿E1
    * A7赋值:成交量(手)>成交量(手)的5日简单移动平均
    * YT赋值:A1 AND A2 AND A3 AND A4 AND A5 AND A6 AND A7
    * LYT赋值:YT的10日过滤
    * FXG赋值:上市的天数>100
    * NTP赋值:总量>0
    * LYT AND FXG AND NTP


    ``` tdx
    E1:=EMA(C,13);
    E2:=EMA(C,55);
    A1:=COUNT(E1<REF(E1,1),5)>=3 AND E1>REF(E1,1);
    A2:=COUNT(E2>REF(E2,1),13)>=8 AND E2>REF(E2,1);
    A3:=LLV((L/E2-1),13)<=0.1;
    A4:=COUNT(E1>E2,13)=13;
    A5:=COUNT(C>E2,5)=5;
    A6:=CROSS(C,E1);
    A7:=V>MA(V,5);
    YT:=A1 AND A2 AND A3 AND A4 AND A5 AND A6 AND A7;
    LYT:=FILTER(YT,10);
    FXG:=FINANCE(42)>100;
    NTP:=DYNAINFO(8)>0;
    LYT AND FXG AND NTP;
    ```
!!! note "沪深涨跌停选股"
    * ISST股赋值:品种名称中包含"ST"
    * 涨停10赋值:取反 AND 最高价=计算涨停价 AND 最高价>0
    * 涨停5赋值:ISST股 AND 最高价=计算涨停价 AND 最高价>0
    * 所有涨停赋值:(涨停10 OR 涨停5) AND (包括一字板 OR (取反 AND 最高价不等于最低价))
    * 当前涨停赋值:所有涨停 AND 买价(即买一价)和现价的较大值=最高价 AND 卖量(即卖一量)<1
    * 仅盘中涨停赋值:所有涨停 AND 取反
    * 跌停10赋值:取反 AND 最低价=计算跌停价 AND 最低价>0
    * 跌停5赋值:ISST股 AND 最低价=计算跌停价 AND 最低价>0
    * 所有跌停赋值:(跌停10 OR 跌停5) AND (包括一字板 OR (取反 AND 最高价不等于最低价))
    * 当前跌停赋值:所有跌停 AND 卖价(即卖一价)和现价的较小值=最低价 AND 买量(即买一量)<1
    * 仅盘中跌停赋值:所有跌停 AND 取反
    * 涨停RET赋值:如果涨跌停类型=0,返回所有涨停,否则返回如果涨跌停类型=1,返回当前涨停,否则返回仅盘中涨停
    * 跌停RET赋值:如果涨跌停类型=3,返回所有跌停,否则返回如果涨跌停类型=4,返回当前跌停,否则返回仅盘中跌停
    * 如果涨跌停类型<3,返回涨停RET,否则返回跌停RET

    ``` tdx
    ISST股:=NAMEINCLUDE('ST');
    涨停10:=NOT(ISST股) AND DYNAINFO(5)=ZTPRICE(DYNAINFO(3),IF((FINANCE(3)=4 OR FINANCE(3)=3),0.2,0.1)) AND DYNAINFO(5)>0;
    涨停5:=ISST股 AND DYNAINFO(5)=ZTPRICE(DYNAINFO(3),0.05) AND DYNAINFO(5)>0;
    所有涨停:=(涨停10 OR 涨停5) AND (包括一字板 OR (NOT(包括一字板) AND DYNAINFO(5)!=DYNAINFO(6)));
    当前涨停:=所有涨停 AND MAX(DYNAINFO(20),DYNAINFO(7))=DYNAINFO(5) AND DYNAINFO(59)<1;
    仅盘中涨停:=所有涨停 AND NOT(当前涨停);
    跌停10:=NOT(ISST股) AND DYNAINFO(6)=DTPRICE(DYNAINFO(3),IF((FINANCE(3)=4 OR FINANCE(3)=3),0.2,0.1)) AND DYNAINFO(6)>0;
    跌停5:=ISST股 AND DYNAINFO(6)=DTPRICE(DYNAINFO(3),0.05) AND DYNAINFO(6)>0;
    所有跌停:=(跌停10 OR 跌停5) AND (包括一字板 OR (NOT(包括一字板) AND DYNAINFO(5)!=DYNAINFO(6)));
    当前跌停:=所有跌停 AND MIN(DYNAINFO(21),DYNAINFO(7))=DYNAINFO(6) AND DYNAINFO(58)<1;
    仅盘中跌停:=所有跌停 AND NOT(当前跌停);
    涨停RET:=IF(涨跌停类型=0,所有涨停,IF(涨跌停类型=1,当前涨停,仅盘中涨停));
    跌停RET:=IF(涨跌停类型=3,所有跌停,IF(涨跌停类型=4,当前跌停,仅盘中跌停));
    IF(涨跌停类型<3,涨停RET,跌停RET);
    ```